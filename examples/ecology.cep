
animal : type.
wolf : animal.
rabbit : animal.

eats animal animal : bwd.
eats wolf rabbit.

age : type.
young : age.
mature : age.
old : age.

hunger_level : type.
hungry : hunger_level.
sated : hunger_level.

denizen animal age hunger_level : pred.

stage sim = {

eat :
    denizen wolf A hungry
  * denizen rabbit A' sated
-o
    denizen wolf A sated.

get_hungry :
    denizen wolf A sated
-o
    denizen wolf A hungry.

grow_young_mature:
    denizen S young sated -o denizen S mature hungry.

grow_mature_old :
    denizen S mature H -o denizen S old H.

die_old :
    denizen S old _ -o ().

die_hungry :
    denizen S _ hungry -o ().

reproduce :
    $denizen S mature H * $denizen S mature H
-o
    denizen S young hungry.
}
% #interactive sim.

context init = {
  denizen rabbit mature sated,
  denizen rabbit mature sated,
  denizen rabbit mature sated,
  denizen rabbit mature sated,
  denizen wolf young sated,
  denizen wolf mature sated,
  denizen wolf mature sated
}.

#trace _ sim init.

