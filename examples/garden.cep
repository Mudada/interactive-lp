% slightly bigger garden sim
% in this version, we'll try to introduce plants that can
%  - overtake other plants (overgrowth)
%  - require less water (overwatering kills them)
%
% to do this, each plant needs to record:
% - how many days it's gone w/o water
% - how many days in a row it's been watered

nat : type.
z : nat.
s nat : nat.

day nat : pred.

plant_type : type.
flower : plant_type.
herb : plant_type.
vine : plant_type.
succulent : plant_type.

plant_phase : type.
seed : plant_phase.
seedling : plant_phase.
mature : plant_phase.

next plant_phase plant_phase : bwd.
next seed seedling.
next seedling mature.

health : type.
healthy : health.
overgrowing : health.
wilting : health.
dead : health.

harvested plant_type : pred.
have_seed plant_type : pred.
% last var is for synchronization
plant plant_type plant_phase health nat : pred.

step : pred.

soil_space : pred.

% actions: water, plant, harvest
stage play {
  get_seeds : $step * harvested Type -o have_seed Type * have_seed Type.

  plant : 
    $step * have_seed Type * soil_space * $day D 
      -o plant Type seed healthy D.
  
  harvest : $step * plant Type mature healthy _ 
      -o harvested Type * soil_space.
  
  clear : $step * plant Type _ dead _
      -o soil_space.
  
  water : $step * plant Type Phase wilting D 
      -o plant Type Phase healthy D.
  
  done : step -o ().
}
#interactive play.

qui * stage play -o stage advance_day * step.

stage advance_day = {
  advance : day N * step -o day (s N).
}

qui * stage advance_day -o stage grow.

stage grow = {
  grow : plant Type Phase healthy D * next Phase Phase' * $day (s D)
            -o plant Type Phase' healthy (s D).
  wilt : plant Type Phase healthy D * $day (s D)
            -o plant Type Phase wilting (s D).
  die : plant Type Phase wilting D * $day (s D)
            -o plant Type Phase dead (s D).
}
qui * stage grow -o stage play * step.

context init = {day z, 
  have_seed flower, have_seed herb, have_seed vine, have_seed succulent,
  step, soil_space, soil_space, soil_space, soil_space}.
#trace _ play init.


